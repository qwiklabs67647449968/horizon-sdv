// Copyright (c) 2024-2025 Accenture, All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//         http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Description:
// AAOS Build targets pipeline.

def kubernetesPodTemplate = """
        apiVersion: v1
        kind: Pod
        metadata:
          annotations:
            cluster-autoscaler.kubernetes.io/safe-to-evict: "true"
          labels:
            aaos_pod: "true"
        spec:
          tolerations:
          - key: workloadType
            operator: Equal
            value: android
            effect: "NoSchedule"
          affinity:
            podAntiAffinity:
              requiredDuringSchedulingIgnoredDuringExecution:
              - labelSelector:
                  matchExpressions:
                  - key: aaos_pod
                    operator: Exists
                topologyKey: kubernetes.io/hostname
          hostname: jenkins-aaos-build-pod
          serviceAccountName: ${JENKINS_SERVICE_ACCOUNT}
          containers:
          - name: builder
            image: ${CLOUD_REGION}-docker.pkg.dev/${CLOUD_PROJECT}/${ANDROID_BUILD_DOCKER_ARTIFACT_PATH_NAME}:latest
            imagePullPolicy: IfNotPresent
            command:
            - sleep
            args:
            - 4h
            resources:
              limits:
                cpu: 96000m
                memory: 160000Mi
              requests:
                cpu: 96000m
                memory: 160000Mi
            volumeMounts:
              - mountPath: /aaos-cache
                name: aaos-cache
          volumes:
            - name: aaos-cache
              ephemeral:
                volumeClaimTemplate:
                  spec:
                    storageClassName: ${JENKINS_CACHE_STORAGE_CLASS_NAME}
                    resources:
                      requests:
                        storage: 2000Gi
                    accessModes:
                      - ReadWriteOnce
          nodeSelector:
            workloadLabel: android
""".stripIndent()

pipeline {

  parameters {
    string(name: 'AAOS_GERRIT_MANIFEST_URL',
           defaultValue: "https://${env.HORIZON_DOMAIN}/gerrit/android/platform/manifest",
           description: "<p>Android Manifest URL.</p>")

    string(name: 'AAOS_REVISION',
           defaultValue: 'horizon/android-14.0.0_r30',
           description: '''<p>Android revision tag/branch name.</p>''')

    string(name: 'AAOS_LUNCH_TARGET',
           defaultValue: '',
           description: '''<p>Build Android cuttlefish, virtual devices, Pixel and RPi target to build.</p>''')

    choice(name: 'ANDROID_VERSION',
           choices: ['14', '15'],
           description: '''<p>Android Version for AVD image builds.<br/>
           Essential for <b>Android Studio</b> devices <i>sdk_car</i> targets to create the SDK addons and device files</p>''')

    string(name: 'POST_REPO_INITIALISE_COMMAND',
           defaultValue: '',
           description: '''<p>Optional additional commands post repo init and prior to repo sync, build/make.<br/>
           <b>Note: </b>Single command line only, use logical operators to execute subsequent commands.<br/><br/></p>''')

    string(name: 'POST_REPO_SYNC_COMMAND',
           defaultValue: '',
           description: '''<p>Optional additional commands post repo sync/fetch and prior to build/make.<br/>
           <b>Note: </b>Single command line only, use logical operators to execute subsequent commands.<br/><br/></p>''')

    string(name: 'OVERRIDE_MAKE_COMMAND',
           defaultValue: '',
           description: '''<p>Optional override default make command.<br/>
           <b>Note: </b>Single command line only, use logical operators to execute subsequent commands.</p>''')

    choice(name: 'AAOS_CLEAN',
           choices: ['NO_CLEAN', 'CLEAN_BUILD', 'CLEAN_ALL'],
           description: '''<p>Clean build or cache directories, e.g.
           <ul>
           <li>NO_CLEAN : do not clean</li>
           <li>CLEAN_BUILD : this will clean the build target output directory</li>
           <li>CLEAN_ALL : this will clear the whole cache including source</li>
           </ul>
           <b>Warning:</b> Only use when necessary.</p>''')

    string(name: 'GERRIT_REPO_SYNC_JOBS',
           defaultValue: "${env.REPO_SYNC_JOBS}",
           description: '''<p>Number of parallel sync jobs for <i>repo sync</i>.<br/>
           Default set to <tt>REPO_SYNC_JOBS</tt> environment variable value.<br/>
           If undefined (or null), defaults to 1.</p>''')

    choice(name: 'INSTANCE_RETENTION_TIME',
           choices: ['0', '15', '30', '45', '60'],
           description: '''<p>Time in minutes to retain the instance after build completion.<br/>
           Useful for debugging build issues, reviewing target outputs etc.</p>''')

    string(name: 'AAOS_ARTIFACT_STORAGE_SOLUTION', defaultValue: 'GCS_BUCKET',
           description: '''<p>Android Artifact Storage:<br/>
           <ul><li>GCS_BUCKET will store to cloud bucket storage</li>
           <li>Empty will result in nothing stored</li></ul></p>''')

    // Fetch patchset from upstream open review change.
    string(name: 'GERRIT_PROJECT', defaultValue: '',
           description: '<p>Optional, define Gerrit Project with open review.</p>')
    string(name: 'GERRIT_CHANGE_NUMBER', defaultValue: '',
           description: '<p>Optional, define Gerrit review item change number.</p>')
    string(name: 'GERRIT_PATCHSET_NUMBER', defaultValue: '',
           description: '<p>Optional, define Gerrit review item patchset number.</p>')
  }

  agent none

  stages {
    stage ('Start VM Instance') {
      agent { kubernetes { yaml kubernetesPodTemplate } }
      stages {
        stage ('Clean') {
          when {
            allOf {
              expression { env.AAOS_LUNCH_TARGET }
              expression { env.AAOS_CLEAN != 'NO_CLEAN' }
            }
          }
          steps {
            script {
              currentBuild.description = "${env.AAOS_LUNCH_TARGET}" + "<br>" + "${env.AAOS_REVISION}" + "<br>" + "$BUILD_USER"
            }
            container(name: 'builder') {
              catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                sh '''
                  AAOS_LUNCH_TARGET="${AAOS_LUNCH_TARGET}" \
                  AAOS_CLEAN="${AAOS_CLEAN}" \
                  ./workloads/android/pipelines/builds/aaos_builder/aaos_environment.sh
                '''
              }
            }
          }
        }

        stage ('Initialise') {
          when {
            allOf {
              expression { env.AAOS_LUNCH_TARGET }
              expression { currentBuild.currentResult == 'SUCCESS' }
            }
          }
          steps {
            script {
              currentBuild.description = "${env.AAOS_LUNCH_TARGET}" + "<br>" + "${env.AAOS_REVISION}" + "<br>" + "$BUILD_USER"
            }
            container(name: 'builder') {
              catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                withCredentials([usernamePassword(credentialsId: env.GERRIT_CREDENTIALS_ID, passwordVariable: 'GERRIT_PASSWORD', usernameVariable: 'GERRIT_USERNAME')]) {
                  sh '''
                    set +x
                    echo "AAOS CACHE Persistent Volume Claim: ${NODE_NAME}-aaos-cache" | tee -a build_cache_volume.txt
                    /usr/bin/kubectl get pod ${NODE_NAME} -n jenkins -o=jsonpath='{.spec.nodeName}' | xargs -I {} gcloud compute instances describe {} --zone=${CLOUD_ZONE} | grep 'deviceName: pvc' | awk '{print "AAOS CACHE Persistent Volume: " $2}' | tee -a build_cache_volume.txt || true
                  '''
                  sh '''
                    git config --global credential.helper store
                    git config --global url."https://${GERRIT_USERNAME}@${HORIZON_DOMAIN}/gerrit".insteadOf "https://${HORIZON_DOMAIN}/gerrit"
                    echo https://"${GERRIT_USERNAME}":"${GERRIT_PASSWORD}"@"${HORIZON_DOMAIN}/gerrit" > ~/.git-credentials

                    AAOS_GERRIT_MANIFEST_URL="${AAOS_GERRIT_MANIFEST_URL}" \
                    AAOS_REVISION="${AAOS_REVISION}" \
                    AAOS_LUNCH_TARGET="${AAOS_LUNCH_TARGET}" \
                    AAOS_CLEAN="NO_CLEAN" \
                    POST_REPO_INITIALISE_COMMAND="${POST_REPO_INITIALISE_COMMAND}" \
                    POST_REPO_SYNC_COMMAND="${POST_REPO_SYNC_COMMAND}" \
                    REPO_SYNC_JOBS="${GERRIT_REPO_SYNC_JOBS}" \
                    ./workloads/android/pipelines/builds/aaos_builder/aaos_initialise.sh
                  '''
                }
              }
              archiveArtifacts artifacts: 'build_cache*.txt', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
            }
          }
        }

        stage ('Build'){
          when {
            allOf {
              expression { env.AAOS_LUNCH_TARGET }
              expression { currentBuild.currentResult == 'SUCCESS' }
            }
          }
          steps {
            container(name: 'builder') {
              catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                sh '''
                  AAOS_LUNCH_TARGET="${AAOS_LUNCH_TARGET}" \
                  ./workloads/android/pipelines/builds/aaos_builder/aaos_build.sh
                '''
              }
            }
          }
        }

        stage ('Storage') {
          when {
            allOf {
              expression { env.AAOS_LUNCH_TARGET }
              expression { env.AAOS_ARTIFACT_STORAGE_SOLUTION }
              expression { currentBuild.currentResult == 'SUCCESS' }
            }
          }
          steps {
            container(name: 'builder') {
              catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
                sh '''
                  AAOS_LUNCH_TARGET="${AAOS_LUNCH_TARGET}" \
                  AAOS_REVISION="${AAOS_REVISION}" \
                  ./workloads/android/pipelines/builds/aaos_builder/aaos_avd_sdk.sh || true

                  AAOS_LUNCH_TARGET="${AAOS_LUNCH_TARGET}" \
                  AAOS_ARTIFACT_ROOT_NAME="${ANDROID_BUILD_BUCKET_ROOT_NAME}" \
                  AAOS_ARTIFACT_STORAGE_SOLUTION="${AAOS_ARTIFACT_STORAGE_SOLUTION}" \
                  ./workloads/android/pipelines/builds/aaos_builder/aaos_storage.sh
                '''
                archiveArtifacts artifacts: '*artifacts*.txt', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
              }
            }
          }
        }

        stage ('Retain instance') {
          when { expression { env.AAOS_LUNCH_TARGET } }
          steps {
            container(name: 'builder') {
              script {
                sleep(time: "${INSTANCE_RETENTION_TIME}", unit: 'MINUTES')
              }
            }
          }
        }
      }
    }
  }
}
