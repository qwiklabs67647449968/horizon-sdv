// Copyright (c) 2024-2025 Accenture, All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//         http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Description:
// Compliance Test Suite Execution on Cuttlefish Virtual Devices.
//
// On error, cleanup VM instances because the GCE plugin can lose connection
// with GCP and VM instance, leaving the instance running and that adds to
// cloud costs.

// Used in cleanup stage should prior stages fail.
def kubernetesPodTemplate = """
  apiVersion: v1
  kind: Pod
  metadata:
    annotations:
      cluster-autoscaler.kubernetes.io/safe-to-evict: "true"
  spec:
    serviceAccountName: ${JENKINS_SERVICE_ACCOUNT}
    containers:
    - name: builder
      image: ${CLOUD_REGION}-docker.pkg.dev/${CLOUD_PROJECT}/${ANDROID_BUILD_DOCKER_ARTIFACT_PATH_NAME}:latest
      imagePullPolicy: Always
      command:
      - sleep
      args:
      - 4h
""".stripIndent()

pipeline {

  parameters {
    string(name: 'JENKINS_GCE_CLOUD_LABEL',
           defaultValue: 'cuttlefish-vm-main',
           description: '''<p>The Jenkins GCE Clouds label for the Cuttlefish instance template, e.g.<br/></p>
           <ul>
             <li>cuttlefish-vm-main</li>
             <li>cuttlefish-vm-v110</li>
           </ul>''')

    string(name: 'CUTTLEFISH_DOWNLOAD_URL',
           defaultValue: '',
           description: "<p>Storage URL pointing to the location of the Cuttlefish Virtual Device images and host packages, e.g.<br/>gs://${env.ANDROID_BUILD_BUCKET_ROOT_NAME}/Android/Builds/AAOS_Builder/&lt;BUILD_NUMBER&gt;</p>")

    choice(name: 'ANDROID_VERSION',
           choices: ['14', '15'],
           description: '''<p>Select Android version: Android 14 or 15<br/>
           Essential for picking the correct test hardness</p>''')

    string(name: 'CTS_DOWNLOAD_URL',
           defaultValue: '',
           description: "<p>Optional CTS test harness download URL.<br/>Use official CTS test harness (empty field) or one built from CTS Builder job and stored in GS Bucket, e.g.<br/>gs://${env.ANDROID_BUILD_BUCKET_ROOT_NAME}/Android/Builds/CTS_Builder/&lt;BUILD_NUMBER&gt;/android-cts.zip</p>")

    string(name: 'CTS_TESTPLAN',
           defaultValue: 'cts-virtual-device-stable',
           description: '''<p>Test plan.<br/>
           The cts-virtual-device-stable test plan tracks the latest of what is stable on the virtual platform.</p>''')

    string(name: 'CTS_MODULE',
           defaultValue: 'CtsHostsideNumberBlockingTestCases',
           description: '''<p>CTS module to test, or leave empty if all modules are to be tested.</p>''')

    string(name: 'CUTTLEFISH_MAX_BOOT_TIME',
           defaultValue: '300',
           description: '''<p>Android Cuttlefish max boot time in seconds.<br/>
           Wait on VIRTUAL_DEVICE_BOOT_COMPLETED across devices.</p>''')

    string(name: 'NUM_INSTANCES',
           defaultValue: '10',
           description: '''<p>Number of guest instances to launch (num-instances option)</p>''')

    string(name: 'VM_CPUS',
           defaultValue: '6',
           description: '''<p>Virtual CPU count (cpus option).</p>''')

    string(name: 'VM_MEMORY_MB',
           defaultValue: '16384',
           description: '''<p>total memory available to guest (memory_mb option)</p>''')

    string(name: 'CTS_TIMEOUT',
           defaultValue: '240',
           description: "<p>CTS Timeout in minutes for each test run.</p>")

    booleanParam(name: 'MTK_CONNECT_ENABLE',
                 defaultValue: false,
                 description: '''<p>Enable if wishing to use MTK Connect to view UI of CTS tests on virtual devices</p>''')

    choice(name: 'CUTTLEFISH_KEEP_ALIVE_TIME',
           choices: ['0', '5', '15', '30', '60', '90', '120', '180'],
           description: '''<p>Time in minutes, to keep CVD alive before stopping the devices and instance.</br>.
           Only applicable when <i>MTK_CONNECT_ENABLE</i> enabled so as to connect via HOST.</p>''')
  }

  agent none

  stages {
    stage ('Start VM Instance') {
      agent { label params.JENKINS_GCE_CLOUD_LABEL }

      stages {
        stage ('Launch Virtual Devices') {
          when { expression { env.CUTTLEFISH_DOWNLOAD_URL } }
          steps {
            script {
              currentBuild.description = "$BUILD_USER"
            }
            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
              script { env.VM_NODE_NAME = env.NODE_NAME }
              script { env.CTS_STAGE = 'false' }
              sh '''
                CUTTLEFISH_DOWNLOAD_URL="${CUTTLEFISH_DOWNLOAD_URL}" \
                CUTTLEFISH_MAX_BOOT_TIME="${CUTTLEFISH_MAX_BOOT_TIME}" \
                NUM_INSTANCES="${NUM_INSTANCES}" \
                VM_CPUS="${VM_CPUS}" \
                VM_MEMORY_MB="${VM_MEMORY_MB}" \
                ./workloads/android/pipelines/tests/cvd_launcher/cvd_start_stop.sh --start
              '''
            }
          }
        }

        stage ('MTK Connect to Virtual Devices') {
          when {
            allOf {
              expression { env.MTK_CONNECT_ENABLE == 'true' }
              expression { env.CUTTLEFISH_DOWNLOAD_URL }
              expression { currentBuild.currentResult == 'SUCCESS' }
            }
          }
          // MTKC installer may error and hang on dpkg lock, so timeout after 15m.
          steps {
            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
              withCredentials([usernamePassword(credentialsId: 'jenkins-mtk-connect-apikey', passwordVariable: 'MTK_CONNECT_PASSWORD', usernameVariable: 'MTK_CONNECT_USERNAME')]) {
                sh '''
                  sudo \
                  MTK_CONNECT_DOMAIN=${HORIZON_DOMAIN} \
                  MTK_CONNECT_USERNAME=${MTK_CONNECT_USERNAME} \
                  MTK_CONNECT_PASSWORD=${MTK_CONNECT_PASSWORD} \
                  MTK_CONNECTED_DEVICES="${NUM_INSTANCES}" \
                  MTK_CONNECT_TEST_ARTIFACT="${CUTTLEFISH_DOWNLOAD_URL}" \
                  MTK_CONNECT_TESTBENCH="${JOB_NAME}-${BUILD_NUMBER}" \
                  timeout 15m ./workloads/android/pipelines/tests/cvd_launcher/cvd_mtk_connect.sh --start
                '''
              }
            }
          }
        }

        stage ('CTS execution') {
          when {
            allOf {
              expression { env.CUTTLEFISH_DOWNLOAD_URL }
              expression { currentBuild.currentResult == 'SUCCESS' }
            }
          }
          steps {
            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
              script { env.CTS_STAGE = 'true' }
              sh '''
                ANDROID_VERSION=${ANDROID_VERSION} \
                CTS_DOWNLOAD_URL="${CTS_DOWNLOAD_URL}" \
                ./workloads/android/pipelines/tests/cts_execution/cts_initialise.sh
                CTS_TESTPLAN="${CTS_TESTPLAN}" \
                CTS_MODULE="${CTS_MODULE}" \
                CTS_TIMEOUT="${CTS_TIMEOUT}" \
                SHARD_COUNT="${NUM_INSTANCES}" \
                ./workloads/android/pipelines/tests/cts_execution/cts_execution.sh
              '''

              archiveArtifacts artifacts: 'cts*.txt', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
              archiveArtifacts artifacts: 'android-cts-results/invocation_summary.txt', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
              archiveArtifacts artifacts: 'android-cts-results/*.zip', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
            }
          }
        }

        stage ('Keep Devices Alive') {
          when {
            allOf {
              expression { env.CUTTLEFISH_DOWNLOAD_URL }
              expression { env.MTK_CONNECT_ENABLE == 'true' }
              expression { env.CTS_STAGE == 'true' }
            }
          }
          steps {
            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
              script {
                sleep(time: "${CUTTLEFISH_KEEP_ALIVE_TIME}", unit: 'MINUTES')
              }
            }
          }
        }

        stage ('Stop Virtual Devices') {
          when { expression { env.CUTTLEFISH_DOWNLOAD_URL } }
          steps {
            catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
              withCredentials([usernamePassword(credentialsId: 'jenkins-mtk-connect-apikey', passwordVariable: 'MTK_CONNECT_PASSWORD', usernameVariable: 'MTK_CONNECT_USERNAME')]) {
                script {
                  sh 'echo "Stopping  MTK Connect"'
                  sh '''
                    if [ $MTK_CONNECT_ENABLE = true ]; then
                      sudo \
                      MTK_CONNECT_DOMAIN=${HORIZON_DOMAIN} \
                      MTK_CONNECT_USERNAME=${MTK_CONNECT_USERNAME} \
                      MTK_CONNECT_PASSWORD=${MTK_CONNECT_PASSWORD} \
                      MTK_CONNECTED_DEVICES="${NUM_INSTANCES}" \
                      MTK_CONNECT_TESTBENCH="${JOB_NAME}-${BUILD_NUMBER}" \
                      timeout 10m ./workloads/android/pipelines/tests/cvd_launcher/cvd_mtk_connect.sh --stop || true
                    fi
                  '''
                  sh 'echo "Stopping Cuttlefish"'
                  sh './workloads/android/pipelines/tests/cvd_launcher/cvd_start_stop.sh --stop || true'

                }
                archiveArtifacts artifacts: 'cvd*.log', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
                archiveArtifacts artifacts: 'cuttlefish*.tgz', followSymlinks: false, onlyIfSuccessful: false, allowEmptyArchive: true
              }
            }
          }
        }
      }
    }

    stage ('Cleanup') {
      agent { kubernetes { yaml kubernetesPodTemplate } }
      stages {
        // Remove VM instances on error to avoid instances left running.
        stage ('Remove VM Instance') {
          when { expression { currentBuild.currentResult != 'SUCCESS' } }
          steps {
            container(name: 'builder') {
              sh '''
                echo "Removing " ${VM_NODE_NAME} " on error!" || true
                yes Y | gcloud compute instances delete ${VM_NODE_NAME} --zone ${CLOUD_ZONE} || true
              '''
            }
          }
        }

        stage ('Delete Offline Testbenches') {
          when { expression { currentBuild.currentResult != 'SUCCESS' } }
          steps {
            container(name: 'builder') {
              withCredentials([usernamePassword(credentialsId: 'jenkins-mtk-connect-apikey', passwordVariable: 'MTK_CONNECT_PASSWORD', usernameVariable: 'MTK_CONNECT_USERNAME')]) {
                sh '''
                  sudo \
                  MTK_CONNECT_DOMAIN=${HORIZON_DOMAIN} \
                  MTK_CONNECT_USERNAME=${MTK_CONNECT_USERNAME} \
                  MTK_CONNECT_PASSWORD=${MTK_CONNECT_PASSWORD} \
                  MTK_CONNECT_TESTBENCH="${JOB_NAME}-${BUILD_NUMBER}" \
                  MTK_CONNECT_DELETE_OFFLINE_TESTBENCHES=true \
                  timeout 10m ./workloads/android/pipelines/tests/cvd_launcher/cvd_mtk_connect.sh --delete || true
                '''
              }
            }
          }
        }
      }
    }
  }
}
